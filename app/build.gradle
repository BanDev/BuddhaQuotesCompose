plugins {
    id 'com.android.application'
    id 'org.jetbrains.kotlin.android'
    id 'org.jetbrains.kotlin.kapt'
    id 'com.mikepenz.aboutlibraries.plugin'
    id 'com.google.protobuf'
}
apply plugin: 'dagger.hilt.android.plugin'

android {
    compileSdk 31

    defaultConfig {
        applicationId "org.bandev.buddhaquotescompose"
        minSdk 21
        targetSdk 31
        versionCode 1
        versionName "1.0"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        vectorDrawables.useSupportLibrary true
    }

    buildTypes {
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_11
        targetCompatibility JavaVersion.VERSION_11
    }
    kotlinOptions {
        jvmTarget = JavaVersion.VERSION_11.toString()
        freeCompilerArgs += '-Xopt-in=kotlin.RequiresOptIn'
    }
    buildFeatures {
        compose true
    }
    composeOptions {
        kotlinCompilerExtensionVersion compose_version
    }
    packagingOptions {
        resources {
            excludes += '/META-INF/{AL2.0,LGPL2.1}'
        }
    }
}

dependencies {
    implementation 'androidx.browser:browser:1.4.0'
    implementation 'androidx.core:core-ktx:1.8.0'
    implementation "androidx.compose.ui:ui:$compose_version"
    implementation "androidx.compose.material:material:$compose_version"
    implementation "androidx.compose.ui:ui-tooling-preview:$compose_version"
    implementation "androidx.compose.runtime:runtime-livedata:$compose_version"
    implementation 'androidx.activity:activity-compose:1.5.1'
    implementation "androidx.compose.material:material-icons-extended:$compose_version"
    implementation "androidx.compose.animation:animation:$compose_version"
    implementation 'androidx.navigation:navigation-compose:2.5.1'
    implementation 'androidx.hilt:hilt-navigation-compose:1.0.0'
    implementation 'androidx.core:core-splashscreen:1.0.0'
    testImplementation 'junit:junit:4.13.2'
    androidTestImplementation 'androidx.test.ext:junit:1.1.3'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.4.0'
    androidTestImplementation "androidx.compose.ui:ui-test-junit4:$compose_version"
    debugImplementation "androidx.compose.ui:ui-tooling:$compose_version"

    // AboutLibraries - https://github.com/mikepenz/AboutLibraries
    implementation 'com.mikepenz:aboutlibraries-core:8.9.4'

    // Accompanist - https://github.com/google/accompanist
    def accompanist_version = "0.23.1"
    implementation "com.google.accompanist:accompanist-insets:$accompanist_version"
    implementation "com.google.accompanist:accompanist-insets-ui:$accompanist_version"
    implementation "com.google.accompanist:accompanist-pager:$accompanist_version"
    implementation "com.google.accompanist:accompanist-pager-indicators:$accompanist_version"
    implementation "com.google.accompanist:accompanist-systemuicontroller:$accompanist_version"

    // Compose Icons - https://github.com/DevSrSouza/compose-icons
    implementation 'br.com.devsrsouza.compose.icons.android:simple-icons:1.0.0'

    // Compose Markdown - https://github.com/jeziellago/compose-markdown
    implementation 'com.github.jeziellago:compose-markdown:0.3.0'

    // Compose Settings - https://github.com/alorma/Compose-Settings
    implementation 'com.github.alorma:compose-settings-ui:0.6.0'

    // Datastore
    implementation 'androidx.datastore:datastore:1.0.0'
    implementation "com.google.protobuf:protobuf-javalite:$protobuf_version"

    // Flinger - https://github.com/iamjosephmj/flinger
    implementation 'com.github.iamjosephmj:Flinger:1.1.1'

    // Hilt - https://developer.android.com/training/dependency-injection/hilt-android
    def hilt_version = "2.43.2"
    implementation "com.google.dagger:hilt-android:$hilt_version"
    kapt "com.google.dagger:hilt-compiler:$hilt_version"

    // Kotlin Coroutines - https://github.com/Kotlin/kotlinx.coroutines
    def coroutines_version = "1.6.4"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core:$coroutines_version"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-android:$coroutines_version"

    // Lifecycle - https://developer.android.com/jetpack/androidx/releases/lifecycle
    def lifecycle_version = "2.5.1"
    implementation "androidx.lifecycle:lifecycle-livedata-ktx:$lifecycle_version"
    implementation "androidx.lifecycle:lifecycle-runtime-ktx:$lifecycle_version"
    implementation "androidx.lifecycle:lifecycle-viewmodel-compose:$lifecycle_version"

    // Room - https://developer.android.com/training/data-storage/room
    def room_version="2.4.3"
    implementation "androidx.room:room-runtime:$room_version"
    implementation "androidx.room:room-ktx:$room_version"
    kapt "androidx.room:room-compiler:$room_version"

}

protobuf {
    protoc {
        artifact = "com.google.protobuf:protoc:$protobuf_version"
    }

    // Generates the java Protobuf-lite code for the Protobufs in this project. See
    // https://github.com/google/protobuf-gradle-plugin#customizing-protobuf-compilation
    // for more information.
    generateProtoTasks {
        all().each { task ->
            task.builtins {
                java {
                    option 'lite'
                }
            }
        }
    }
}